apply plugin: 'com.android.application'

buildscript {
  repositories {
     mavenCentral()
  }

  dependencies {
     classpath 'me.tatarka:gradle-retrolambda:3.2.5'
  }
}

repositories {
  mavenCentral()
}

apply plugin: 'me.tatarka.retrolambda'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
	keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.clayster.xmppiotdemo"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName "0.1"
        jackOptions {
            enabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
		if (keystorePropertiesFile.exists()) {
			release {
				keyAlias keystoreProperties['keyAlias']
				keyPassword keystoreProperties['keyPassword']
				storeFile file(keystoreProperties['storeFile'])
				storePassword keystoreProperties['storePassword']
			}
		}
    }

	buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			if (keystorePropertiesFile.exists()) {
				signingConfig signingConfigs.release
			}
        }
        debug {
            minifyEnabled false
        }
    }

}

dependencies {
    testCompile 'junit:junit:4.12'
    compile project(":aSmack")
    compile project(":MemorizingTrustManager")
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    // Disabled java-pinning is currently not used as we use the MemorizingTrustManager
//    compile 'eu.geekplace.javapinning:java-pinning-aar:1.0.1'
}
